/obj/item/clothing/mask/gascig
	name = "gas mask"
	desc = "A face-covering mask that can be connected to an air supply. Good for concealing your identity and with a filter slot to help remove those toxins." //More accurate
	icon_state = "gas_alt"
	clothing_flags = BLOCK_GAS_SMOKE_EFFECT | MASKINTERNALS | GAS_FILTERING
	flags_inv = HIDEEARS|HIDEEYES|HIDEFACE|HIDEFACIALHAIR|HIDESNOUT
	w_class = WEIGHT_CLASS_NORMAL
	inhand_icon_state = "gas_alt"
	armor_type = /datum/armor/mask_gas
	flags_cover = MASKCOVERSEYES | MASKCOVERSMOUTH | PEPPERPROOF
	resistance_flags = NONE
	///Max numbers of installable filters
	var/max_filters = 1
	///List to keep track of each filter
	var/list/gas_filters
	///Type of filter that spawns on roundstart
	var/starting_filter_type = /obj/item/gas_filter
	///Does the mask have an FOV?
	var/has_fov = TRUE
	///Cigarette in the mask
	var/obj/item/cigarette/cig
	voice_filter = "lowpass=f=750,volume=2"

/datum/armor/mask_gas
	bio = 100

/obj/item/clothing/mask/gascig/worn_overlays(mutable_appearance/standing, isinhands)
	. = ..()
	if(!isinhands && cig)
		. += cig.build_worn_icon(default_layer = FACEMASK_LAYER, default_icon_file = 'icons/mob/clothing/mask.dmi')

/obj/item/clothing/mask/gascig/Initialize(mapload)
	. = ..()
	if(!max_filters || !starting_filter_type)
		return

	for(var/i in 1 to max_filters)
		var/obj/item/gas_filter/inserted_filter = new starting_filter_type(src)
		LAZYADD(gas_filters, inserted_filter)
	has_filter = TRUE

/obj/item/clothing/mask/gascig/Destroy()
	QDEL_LAZYLIST(gas_filters)
	return..()

/obj/item/clothing/mask/gascig/equipped(mob/equipee, slot)
	cig?.equipped(equipee, slot)
	return ..()

/obj/item/clothing/mask/gascig/adjust_visor(mob/living/user)
	if(!isnull(cig))
		balloon_alert(user, "cig in the way!")
		return FALSE
	return ..()

/obj/item/clothing/mask/gascig/examine(mob/user)
	. = ..()
	if(cig)
		. += span_notice("There is a [cig.name] jammed into the filter slot.")
	if(max_filters > 0 && !cig)
		. += span_notice("[src] has [max_filters] slot\s for filters.")
	if(LAZYLEN(gas_filters) > 0)
		. += span_notice("Currently there [LAZYLEN(gas_filters) == 1 ? "is" : "are"] [LAZYLEN(gas_filters)] filter\s with [get_filter_durability()]% durability.")
		. += span_notice("The filters can be removed by right-clicking with an empty hand on [src].")

/obj/item/clothing/mask/gascig/Exited(atom/movable/gone)
	. = ..()
	if(gone == cig)
		cig = null
		if(ismob(loc))
			var/mob/wearer = loc
			wearer.update_worn_mask()

/obj/item/clothing/mask/gascig/attackby(obj/item/tool, mob/user)
	var/valid_wearer = ismob(loc)
	var/mob/wearer = loc
	if(istype(tool, /obj/item/cigarette))
		if(flags_cover & MASKCOVERSMOUTH)
			balloon_alert(user, "mask's mouth is covered!")
			return ..()

		if(max_filters <= 0 || cig)
			balloon_alert(user, "can't hold that!")
			return ..()

		if(has_filter)
			balloon_alert(user, "filters in the mask!")
			return ..()

		cig = tool
		if(valid_wearer)
			cig.equipped(loc, wearer.get_slot_by_item(cig))

		cig.forceMove(src)
		if(valid_wearer)
			wearer.update_worn_mask()
		return TRUE

	if(cig)
		var/cig_attackby = cig.attackby(tool, user)
		if(valid_wearer)
			wearer.update_worn_mask()
		return cig_attackby
	if(!istype(tool, /obj/item/gas_filter))
		return ..()
	if(LAZYLEN(gas_filters) >= max_filters)
		return ..()
	if(!user.transferItemToLoc(tool, src))
		return ..()
	LAZYADD(gas_filters, tool)
	has_filter = TRUE
	return TRUE

/obj/item/clothing/mask/gascig/attack_hand_secondary(mob/user, list/modifiers)
	if(cig)
		user.put_in_hands(cig)
		cig = null
		if(ismob(loc))
			var/mob/wearer = loc
			wearer.update_worn_mask()
		return SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN
	if(!has_filter || !max_filters)
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	for(var/i in 1 to max_filters)
		var/obj/item/gas_filter/filter = locate() in src
		if(!filter)
			continue
		user.put_in_hands(filter)
		LAZYREMOVE(gas_filters, filter)
	if(LAZYLEN(gas_filters) <= 0)
		has_filter = FALSE
	return SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN

///Check _masks.dm for this one
/obj/item/clothing/mask/gascig/consume_filter(datum/gas_mixture/breath)
	if(LAZYLEN(gas_filters) <= 0 || max_filters == 0)
		return breath
	var/obj/item/gas_filter/gas_filter = pick(gas_filters)
	var/datum/gas_mixture/filtered_breath = gas_filter.reduce_filter_status(breath)
	if(gas_filter.filter_status <= 0)
		LAZYREMOVE(gas_filters, gas_filter)
		qdel(gas_filter)
	if(LAZYLEN(gas_filters) <= 0)
		has_filter = FALSE
	return filtered_breath

/// Initializes the FoV component for the gas mask
/obj/item/clothing/mask/gascig/proc/init_fov()
	if (has_fov)
		AddComponent(/datum/component/clothing_fov_visor, FOV_90_DEGREES)

/**
 * Getter for overall filter durability, takes into consideration all filters filter_status
 */
/obj/item/clothing/mask/gascig/proc/get_filter_durability()
	var/max_filters_durability = LAZYLEN(gas_filters) * 100
	var/current_filters_durability
	for(var/obj/item/gas_filter/gas_filter as anything in gas_filters)
		current_filters_durability += gas_filter.filter_status
	var/durability = (current_filters_durability / max_filters_durability) * 100
	return durability


/obj/item/clothing/mask/gas/clown_hat/cursed
	resistance_flags = FIRE_PROOF | ACID_PROOF
	equip_delay_other = 5 SECONDS
	var/mob/living/carbon/human/victim
	// how long till we can be taken back off?
	var/unstick_time = 3 MINUTES
	// interval between effects
	var/curse_timer = 10 SECONDS
	// are we actively cursing someone?
	var/cursing = FALSE

/obj/item/clothing/mask/gas/clown_hat/cursed/equipped(mob/user, slot)
	. = ..()
	if(slot_flags & slot)
		var/mob/living/M = user
		if(M.mind?.assigned_role == "Clown" || M.mind?.has_antag_datum(/datum/antagonist/nukeop/clownop))
			cursing = FALSE
			return
		else
			ADD_TRAIT(src, TRAIT_NODROP, STICKY_CLOTHING_TRAIT)
			addtimer(CALLBACK(src, PROC_REF(unstick)), unstick_time)
			addtimer(CALLBACK(src, PROC_REF(curse_effect)), curse_timer)
			cursing = TRUE

/obj/item/clothing/mask/gas/clown_hat/cursed/dropped()
	. = ..()
	cursing = FALSE

/obj/item/clothing/mask/gas/clown_hat/cursed/proc/unstick()
	REMOVE_TRAIT(src, TRAIT_NODROP, STICKY_CLOTHING_TRAIT)

/obj/item/clothing/mask/gas/clown_hat/cursed/proc/curse_effect(mob/user, slot)
	if(!cursing)
		return

	var/mob/living/carbon/human/wearer = loc
	if(istype(wearer) && (wearer.get_slot_by_item(src) & slot_flags))
		addtimer(CALLBACK(src, PROC_REF(curse_effect)), curse_timer)

		if(wearer.health >= 0)
			wearer.adjustFireLoss(5)

		var/effectselect = rand(1, 4)
		switch(effectselect)
			if(1)
				//uncontrollable laughin
				wearer.emote("laugh")
			if(2)
				//confused movement
				wearer.adjust_confusion_up_to(3 SECONDS, 10 SECONDS)
			if(3)
				//bouts of Tiredness and Stun
				wearer.adjustStaminaLoss(30)
				wearer.Stun(2 SECONDS)
				wearer.Knockdown(2 SECONDS)
			if(4)
				//minor brain damage
				wearer.adjustOrganLoss(ORGAN_SLOT_BRAIN, 10)
	else
		cursing = FALSE
